declaration = {
      ("let " ~ mutable_specifier? ~ identifier ~ (":" ~ datatype)? ~ ("=" ~ expr)?
        | "static" ~ mutable_specifier? ~ identifier ~ ":" ~ datatype ~ "=" ~ expr) ~ ";"
}

mutable_specifier = { "mut " }

datatype = { "i32" | "bool" | "str" | "()" | reference_datatype }

reference_datatype = { "&" ~ lifetime_type_variable? ~ mutable_specifier? ~ datatype }

block = { "{" ~ sequence ~ expr? ~ "}" }

sequence = { (stmt | block)* }

stmt = { declaration | function_declaration | expr_stmt }

expr_stmt = { expr ~ ";" }

expr = { assignment }

primary = { integer_literal | string_literal | boolean_literal | "(" ~ expr ~ ")" | block | function_app | return_val | identifier }

assignment = { identifier ~ "=" ~ assignment | disjunction }

disjunction = { conjunction ~ ("||" ~ conjunction)* }

conjunction = { equality ~ ("&&" ~ equality)* }

equality = { comparison ~ (("!=" | "==") ~ comparison)* }

comparison = { term ~ ((">" | ">=" | "<" | "<=") ~ term)* }

term = { factor ~ (("-" | "+") ~ factor)* }

factor = { unary ~ (("/" | "*") ~ unary)* }

unary = { ("!" | "-" | "&mut " | "&" | "*") ~ unary | primary }

return_val = { "return " ~ expr }

identifier = @{
      keyword ~ (ASCII_ALPHANUMERIC | "_")
    | !keyword ~ !ASCII_DIGIT ~ (ASCII_ALPHANUMERIC | "_")+
}

function_declaration = {
      "fn " ~ identifier ~ ("<" ~ lifetime_param_list ~ ">")? ~ "(" ~ function_param_list ~ ")" ~
      function_return_type? ~ block
}

function_return_type = { "->" ~ datatype }

lifetime_param_list = { lifetime_type_variable ~ ("," ~ lifetime_type_variable)* | "" }
lifetime_type_variable = @{ "'" ~ !ASCII_DIGIT ~ ("_" ~ (ASCII_ALPHANUMERIC | "_")+ | !"_" ~ (ASCII_ALPHANUMERIC | "_")+) }

function_param_list = { function_param ~ ("," ~ function_param)* | "" }
function_param = { identifier ~ ":" ~ datatype }

function_app = { identifier ~ "(" ~ function_arg_list ~ ")" }
function_arg_list = { expr ~ ("," ~ expr)* | "" }

keyword = _{
    boolean_literal
    | "as"
    | "break"
    | "const"
    | "continue"
    | "crate"
    | "else"
    | "enum"
    | "extern"
    | "fn"
    | "for"
    | "if"
    | "impl"
    | "in"
    | "let"
    | "loop"
    | "match"
    | "mod"
    | "move"
    | "mut"
    | "pub"
    | "ref"
    | "return"
    | "self"
    | "Self"
    | "static"
    | "struct"
    | "super"
    | "trait"
    | "type"
    | "unsafe"
    | "use"
    | "where"
    | "while"
}

boolean_literal = { "true" | "false" }

integer_literal = @{ "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) }

string_literal = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" | "//" ~ (!NEWLINE ~ ANY)* }
